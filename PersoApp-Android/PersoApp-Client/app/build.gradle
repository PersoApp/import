apply plugin: 'com.android.application'
apply plugin: 'hugo'
apply plugin: 'jarjar'
apply plugin: 'android-apt'
dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'de.greenrobot:eventbus:2.0.2'
    compile 'net.vrallev.android.library:android-base:1.5.0'
    compile 'net.vrallev.android.library:crouton-extension:1.0.5'
    compile 'com.squareup.dagger:dagger:1.2.1'
    apt 'com.squareup.dagger:dagger-compiler:1.2.1'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.android.support:support-v4:21.0.0'
    jarjar 'de.persoapp:persoapp-core:1.0.1'
    jarjar 'de.persoapp:persoapp-jaxb:1.0.0'
    jarjar 'org.bouncycastle:bcprov-jdk15on:1.51'
    jarjar('javax.xml.bind:jaxb-api:2.2.1') {
        exclude module: 'stax-api'
        exclude module: 'activation'
    }
    jarjar('javax.xml.ws:jaxws-api:2.2') {
        exclude module: 'jaxb-api'
        exclude module: 'saaj-api'
        exclude module: 'jsr250-api'
        exclude module: 'jsr181-api'
    }
    jarjar 'javax.jws:jsr181-api:1.0-MR1'
    jarjar 'javax.annotation:jsr250-api:1.0'
}
jarjar {
    jarJarFile '../../tools/jarjar-1.4.jar'

    rules = [
            'rule javax.xml.bind.** com.sun.javax.xml.bind.@1',
            'rule javax.xml.soap.** com.sun.javax.xml.soap.@1',
            'rule javax.xml.ws.** com.sun.javax.xml.ws.@1',
            'rule com.sun.xml.bind.** com.sun.xml.internal.bind.@1',
            'rule org.bouncycastle.** de.persoapp.org.bouncycastle.@1'
    ]

    srcExcludes = [
            'META-INF/*.SF',
            'META-INF/MANIFEST.MF',
            'META-INF/LICENSE.txt',
            'javax/xml/ws/wsaddressing/package-info.class'
    ]
}
android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 21
        versionCode 1
        versionName '1.0.1'
    }

    signingConfigs {
        releaseConfig {
            storeFile file("../../tools/Default.keystore")
            storePassword "123456"
            keyAlias "Ageto"
            keyPassword "123456"
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.releaseConfig
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' Debug'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}
apply from: '../eclipse-compatibility.gradle'

repositories {
}