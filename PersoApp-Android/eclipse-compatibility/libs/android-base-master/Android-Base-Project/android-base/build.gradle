apply plugin: 'android-library'
apply plugin: 'maven'

version = '1.5.0'
group = 'net.vrallev.android.library'

dependencies {
    provided 'com.facebook.crypto:conceal:1.0.0'
    compile 'com.android.support:support-v4:+'

    compile 'com.squareup.dagger:dagger:1.2.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.1'
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
        versionCode 10
        versionName=version
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                def mavenRepositoryUrl
                if (!project.hasProperty('mvnRepoUrl')) {
                    println "Settings for mvnRepoUrl are missing/incomplete for ${project.name}"
                    mavenRepositoryUrl = mavenLocal().getUrl()
                } else {
                    mavenRepositoryUrl = project.getProperties().get('mvnRepoUrl')
                }

//                mavenRepositoryUrl = "file:///${System.env.HOME}/.m2/repository"

                repository(url: mavenRepositoryUrl)

                pom.project {
                    name 'Android Base Library'
                    packaging 'aar'
                    description 'An collection of several helper classes and methods'
                    url 'https://github.com/vRallev/android-base'

                    scm {
                        url 'scm:git@github.com:vRallev/android-base.git'
                        connection 'scm:git@github.com:vRallev/android-base.git'
                        developerConnection 'scm:git@github.com:vRallev/android-base.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'vRallev'
                            name 'Ralf Wondratschek'
                            email 'r.wondr@gmail.com'
                        }
                    }
                }
            }
        }
    }

    //noinspection GroovyAssignabilityCheck
    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.allJava
    }

    //noinspection GroovyAssignabilityCheck
    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        //basename = artifact_id
        from androidJavadocs.destinationDir
    }

    //noinspection GroovyAssignabilityCheck
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        //basename = artifact_id
        from android.sourceSets.main.allSource
    }

    artifacts {
        //archives packageReleaseJar
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}

apply from: '../eclipse-compatibility.gradle'